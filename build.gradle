plugins {
    id "com.github.spotbugs" version "4.6.0"
    id 'jacoco'
}

repositories {
    jcenter()
}

def static isJacocoReportTarget(project) {
    if (project == project.rootProject) {
        return false
    }
    if (!project.plugins.hasPlugin('jacoco')) {
        return false
    }
    return true
}

task jacocoMerge(type: JacocoMerge) {
    gradle.afterProject { project, state ->
        if (!isJacocoReportTarget(project)) {
            return
        }
        executionData project.tasks.test.jacoco.destinationFile
        dependsOn project.tasks.test
    }
}

task jacocoMergedReport(type: JacocoReport, dependsOn: [jacocoMerge]) {
    executionData jacocoMerge.destinationFile
    gradle.afterProject { project, state ->
        if (!isJacocoReportTarget(project)) {
            return
        }
        sourceDirectories.from(files([project.sourceSets.main.allJava.srcDirs]))
        classDirectories.from(project.sourceSets.main.output)
    }
    reports {
        xml.enabled = true
        csv.enabled = true
    }
}

task jacocoSummary(type: JacocoSummaryReport, dependsOn: [jacocoMergedReport]) {
    jacocoXmlFile = jacocoMergedReport.reports.xml.destination
    outputJson = file(jacocoMergedReport.reports.xml.destination.parent + "/jacoco_summary.json")
    outputMd = file(jacocoMergedReport.reports.xml.destination.parent + "/jacoco_summary.md")
    stdout = true
    diffEnabled = false
}

jacocoMerge.finalizedBy jacocoMergedReport
jacocoMergedReport.finalizedBy jacocoSummary
