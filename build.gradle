plugins {
    id "com.github.spotbugs" version "4.6.0"
    id 'jacoco'
    id "com.github.sakata1222.jacoco-markdown" version "1.0.0"
}

repositories {
    jcenter()
}

def static isJacocoReportTarget(project) {
    if (project == project.rootProject) {
        return false
    }
    if (!project.plugins.hasPlugin('jacoco')) {
        return false
    }
    return true
}

task jacocoMerge(type: JacocoMerge) {
    gradle.afterProject { project, state ->
        if (!isJacocoReportTarget(project)) {
            return
        }
        executionData project.tasks.test.jacoco.destinationFile
        dependsOn project.tasks.test
    }
}

task jacocoMergedReport(type: JacocoReport, dependsOn: [jacocoMerge]) {
    executionData jacocoMerge.destinationFile
    gradle.afterProject { project, state ->
        if (!isJacocoReportTarget(project)) {
            return
        }
        sourceDirectories.from(files([project.sourceSets.main.allJava.srcDirs]))
        classDirectories.from(project.sourceSets.main.output)
    }
    reports {
        xml.enabled = true
        csv.enabled = true
    }
}

task jacocoSummary(type: jp.gr.java_conf.spica.plugin.gradle.jacoco.JacocoMarkdownTask) {
    jacocoReportTask jacocoMergedReport // auto configuration for the JacocoReportTask
}

task build {

}

build.finalizedBy jacocoMerge
jacocoMerge.finalizedBy jacocoMergedReport
jacocoMergedReport.finalizedBy jacocoSummary
